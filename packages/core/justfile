set dotenv-load := true

root_dir := justfile_directory()

snapshot-update repo="all":
    #!/usr/bin/env bash
    set -euox pipefail
    cd {{ root_dir }}
    # Handle both "just snapshot-update prisma" and "just snapshot-update repo=prisma"
    repo_name="{{ repo }}"
    repo_name="${repo_name#repo=}"
    if [ "$repo_name" = "all" ]; then
        go test -tags integration ./tests/integration/... -v -timeout 15m -update
    else
        go test -tags integration ./tests/integration/... -v -timeout 15m -update -run "TestSingleFramework/$repo_name"
    fi
    npx prettier --write --cache "**/*.{json,yml,yaml,md}"

sync-docs:
    baedal specvital/specvital.github.io/docs docs --exclude ".vitepress/**"

test target="all":
    #!/usr/bin/env bash
    set -euox pipefail
    cd {{ root_dir }}
    case "{{ target }}" in
      all)
        just test unit
        just test integration
        ;;
      unit)
        go test ./...
        ;;
      integration)
        go test -tags integration ./tests/integration/... -v -timeout 15m
        ;;
      *)
        echo "Unknown target: {{ target }}"
        echo "Available: unit, integration, all"
        exit 1
        ;;
    esac

# Scan a directory for tests (used by /validate-parser command)
scan path:
    go run {{ root_dir }}/scripts/scan.go {{ path }}
