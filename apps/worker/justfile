set dotenv-load := true

root_dir := justfile_directory()

bootstrap: install-docker install-psql install-sqlc

clean-containers:
    docker ps -a --filter "label=org.testcontainers=true" -q | xargs -r docker rm -f

dump-schema:
    PGPASSWORD=postgres pg_dump -h postgres -U postgres -d specvital --schema-only --no-owner --no-privileges -n public | grep -v '^\\\|^SET \|^SELECT ' > src/internal/infra/db/schema.sql
    cp src/internal/infra/db/schema.sql src/internal/testutil/postgres/schema.sql

enqueue *args:
    cd src && go run ./cmd/enqueue {{ args }}

gen-sqlc:
    cd src && sqlc generate

install-psql:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v psql &> /dev/null; then
      DEBIAN_FRONTEND=noninteractive apt-get update && \
        apt-get -y install lsb-release wget && \
        wget --quiet -O - https://www.postgresql.org/media/keys/ACCC4CF8.asc | apt-key add - && \
        echo "deb http://apt.postgresql.org/pub/repos/apt/ $(lsb_release -cs)-pgdg main" | tee /etc/apt/sources.list.d/pgdg.list && \
        apt-get update && \
        apt-get -y install postgresql-client-16
    fi

install-docker:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v docker &> /dev/null; then
      curl -fsSL https://get.docker.com | sh
    fi

install-railway:
    npm install -g @railway/cli

install-sqlc:
    go install github.com/sqlc-dev/sqlc/cmd/sqlc@v1.28.0

# Reset database and apply schema from SQL dump
migrate-local:
    #!/usr/bin/env bash
    set -euo pipefail
    echo "Resetting database..."
    PGPASSWORD=postgres psql -h postgres -U postgres -c "DROP DATABASE IF EXISTS specvital;"
    PGPASSWORD=postgres psql -h postgres -U postgres -c "CREATE DATABASE specvital;"
    echo "Applying schema..."
    PGPASSWORD=postgres psql -h postgres -U postgres -d specvital -f src/internal/infra/db/schema.sql
    echo "Migration complete!"

build target="all":
    #!/usr/bin/env bash
    set -euo pipefail
    cd src
    case "{{ target }}" in
      all)
        go build -o ../bin/analyzer ./cmd/analyzer
        go build -o ../bin/spec-generator ./cmd/spec-generator
        go build -o ../bin/retention-cleanup ./cmd/retention-cleanup
        go build -o ../bin/enqueue ./cmd/enqueue
        echo "Built: bin/analyzer, bin/spec-generator, bin/retention-cleanup, bin/enqueue"
        ;;
      analyzer)
        go build -o ../bin/analyzer ./cmd/analyzer
        ;;
      spec-generator)
        go build -o ../bin/spec-generator ./cmd/spec-generator
        ;;
      retention-cleanup)
        go build -o ../bin/retention-cleanup ./cmd/retention-cleanup
        ;;
      enqueue)
        go build -o ../bin/enqueue ./cmd/enqueue
        ;;
      check)
        go build ./...
        ;;
      *)
        echo "Unknown target: {{ target }}. Use: all, analyzer, spec-generator, retention-cleanup, enqueue, check"
        exit 1
        ;;
    esac

run-analyzer:
    cd src && air

run-spec-generator:
    cd src && air -c .air.spec-generator.toml

run-spec-generator-mock:
    cd src && MOCK_MODE=true air -c .air.spec-generator.toml

run-retention-cleanup:
    cd src && go run ./cmd/retention-cleanup

test target="all":
    #!/usr/bin/env bash
    set -euo pipefail
    cd src
    case "{{ target }}" in
      all)
        TESTCONTAINERS_RYUK_DISABLED=true go test -v ./...
        ;;
      unit)
        go test -v -short ./...
        ;;
      integration)
        TESTCONTAINERS_RYUK_DISABLED=true go test -v -run 'Integration|Repository' ./...
        ;;
      *)
        echo "Unknown target: {{ target }}. Use: unit, integration, all"
        exit 1
        ;;
    esac

tidy:
    cd src && go mod tidy

update-core:
    cd src && GOPROXY=direct go get -u github.com/specvital/core@main && go mod tidy

docker-build service="analyzer":
    docker build -f infra/{{ service }}/Dockerfile -t specvital-{{ service }}:local .
