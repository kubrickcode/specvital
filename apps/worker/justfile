set dotenv-load := true

root_dir := justfile_directory()

clean-containers:
    docker ps -a --filter "label=org.testcontainers=true" -q | xargs -r docker rm -f

dump-schema:
    PGPASSWORD=postgres pg_dump -h postgres -U postgres -d specvital --schema-only --no-owner --no-privileges -n public | grep -v '^\\\|^SET \|^SELECT ' > internal/infra/db/schema.sql
    cp internal/infra/db/schema.sql internal/testutil/postgres/schema.sql

enqueue *args:
    go run ./cmd/enqueue {{ args }}

gen-sqlc:
    sqlc generate

install-railway:
    npm install -g @railway/cli

build target="all":
    #!/usr/bin/env bash
    set -euo pipefail
    case "{{ target }}" in
      all)
        go build -o bin/analyzer ./cmd/analyzer
        go build -o bin/spec-generator ./cmd/spec-generator
        go build -o bin/retention-cleanup ./cmd/retention-cleanup
        go build -o bin/enqueue ./cmd/enqueue
        echo "Built: bin/analyzer, bin/spec-generator, bin/retention-cleanup, bin/enqueue"
        ;;
      analyzer)
        go build -o bin/analyzer ./cmd/analyzer
        ;;
      spec-generator)
        go build -o bin/spec-generator ./cmd/spec-generator
        ;;
      retention-cleanup)
        go build -o bin/retention-cleanup ./cmd/retention-cleanup
        ;;
      enqueue)
        go build -o bin/enqueue ./cmd/enqueue
        ;;
      check)
        go build ./...
        ;;
      *)
        echo "Unknown target: {{ target }}. Use: all, analyzer, spec-generator, retention-cleanup, enqueue, check"
        exit 1
        ;;
    esac

run-analyzer:
    air

run-spec-generator:
    air -c .air.spec-generator.toml

run-spec-generator-mock:
    MOCK_MODE=true air -c .air.spec-generator.toml

run-retention-cleanup:
    go run ./cmd/retention-cleanup

test target="all":
    #!/usr/bin/env bash
    set -euo pipefail
    case "{{ target }}" in
      all)
        TESTCONTAINERS_RYUK_DISABLED=true go test -v ./...
        ;;
      unit)
        go test -v -short ./...
        ;;
      integration)
        TESTCONTAINERS_RYUK_DISABLED=true go test -v -run 'Integration|Repository' ./...
        ;;
      *)
        echo "Unknown target: {{ target }}. Use: unit, integration, all"
        exit 1
        ;;
    esac

tidy:
    go mod tidy

update-core:
    GOPROXY=direct go get -u github.com/specvital/core@main && go mod tidy

docker-build service="analyzer":
    docker build -f infra/{{ service }}/Dockerfile -t specvital-{{ service }}:local .
