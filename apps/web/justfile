set dotenv-load := true

root_dir := justfile_directory()

deps: deps-frontend

deps-frontend:
    cd frontend && pnpm --ignore-workspace install

dump-schema:
    PGPASSWORD=postgres pg_dump -h postgres -U postgres -d specvital --schema-only --no-owner --no-privileges -n public | grep -v '^\\\|^SET \|^SELECT ' > backend/internal/db/schema.sql

gen-sqlc:
    cd backend && sqlc generate

gen-api:
    cd backend && oapi-codegen -config api/oapi-codegen.yaml api/openapi.yaml
    cd frontend && npx openapi-typescript ../backend/api/openapi.yaml -o lib/api/generated-types.ts

lint-frontend:
    cd {{ root_dir }} && just _lint-ts batch

typecheck-file file:
    #!/usr/bin/env bash
    set -euo pipefail
    dir=$(dirname "{{ file }}")
    while [[ "$dir" != "." && "$dir" != "/" ]]; do
      if [[ -f "$dir/tsconfig.json" ]]; then
        (cd "$dir" && npx tsc --noEmit --incremental)
        exit 0
      fi
      dir=$(dirname "$dir")
    done
    if [[ -f "tsconfig.json" ]]; then
      npx tsc --noEmit --incremental
    fi


run target *args:
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      backend)
        cd backend && air
        ;;
      frontend)
        cd frontend && pnpm dev
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac

build target="all":
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      all)
        just build backend
        just build frontend
        ;;
      backend)
        cd backend && go build ./...
        ;;
      frontend)
        cd frontend && pnpm build
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac


test target="all":
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      all)
        just test backend
        just test frontend
        ;;
      backend)
        cd backend && go test -v ./...
        ;;
      frontend)
        cd frontend && pnpm test
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac

test-e2e *args:
    pnpm test:e2e {{ args }}

test-e2e-ui:
    pnpm test:e2e:ui

test-e2e-report:
    pnpm test:e2e:report
