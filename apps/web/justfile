set dotenv-load := true

root_dir := justfile_directory()

deps: deps-frontend

deps-frontend:
    cd src/frontend && pnpm --ignore-workspace install

dump-schema:
    PGPASSWORD=postgres pg_dump -h postgres -U postgres -d specvital --schema-only --no-owner --no-privileges -n public | grep -v '^\\\|^SET \|^SELECT ' > src/backend/internal/db/schema.sql

gen-sqlc:
    cd src/backend && sqlc generate

gen-api:
    cd src/backend && oapi-codegen -config api/oapi-codegen.yaml api/openapi.yaml
    cd src/frontend && npx openapi-typescript ../backend/api/openapi.yaml -o lib/api/generated-types.ts

lint-frontend:
    npx prettier --write --cache "src/frontend/**/*.{ts,tsx}"
    cd src/frontend && npx eslint --fix --max-warnings=0 .

typecheck-file file:
    #!/usr/bin/env bash
    set -euo pipefail
    dir=$(dirname "{{ file }}")
    while [[ "$dir" != "." && "$dir" != "/" ]]; do
      if [[ -f "$dir/tsconfig.json" ]]; then
        (cd "$dir" && npx tsc --noEmit --incremental)
        exit 0
      fi
      dir=$(dirname "$dir")
    done
    if [[ -f "tsconfig.json" ]]; then
      npx tsc --noEmit --incremental
    fi


run target *args:
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      backend)
        cd src/backend && air
        ;;
      frontend)
        cd src/frontend && pnpm dev
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac

build target="all":
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      all)
        just build backend
        just build frontend
        ;;
      backend)
        cd src/backend && go build ./...
        ;;
      frontend)
        cd src/frontend && pnpm build
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac


test target="all":
    #!/usr/bin/env bash
    set -euox pipefail
    case "{{ target }}" in
      all)
        just test backend
        just test frontend
        ;;
      backend)
        cd src/backend && go test -v ./...
        ;;
      frontend)
        cd src/frontend && pnpm test
        ;;
      *)
        echo "Unknown target: {{ target }}"
        exit 1
        ;;
    esac

update-core:
    cd src/backend && GOPROXY=direct go get -u github.com/specvital/core@main && go mod tidy

test-e2e *args:
    pnpm test:e2e {{ args }}

test-e2e-ui:
    pnpm test:e2e:ui

test-e2e-report:
    pnpm test:e2e:report
